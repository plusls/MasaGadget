plugins {
    id("maven-publish")
    id("fabric-loom").version("1.1-SNAPSHOT").apply(false)
    // https://github.com/Juuxel/LoomQuiltflower
    id("io.github.juuxel.loom-quiltflower").version("1.8.0").apply(false)
    id("net.kyori.blossom").version("1.3.1").apply(false)
    id("com.replaymod.preprocess").version("SNAPSHOT")
    id("org.ajoberstar.grgit").version("5.0.0")
}

preprocess {
    def mc1144 = createNode("1.14.4", 1_14_04, "mojang")
    def mc1152 = createNode("1.15.2", 1_15_02, "mojang")
    def mc1165 = createNode("1.16.5", 1_16_05, "mojang")
    def mc1171 = createNode("1.17.1", 1_17_01, "mojang")
    def mc1182 = createNode("1.18.2", 1_18_02, "mojang")
    def mc1192 = createNode("1.19.2", 1_19_02, "mojang")
    def mc1193 = createNode("1.19.3", 1_19_03, "mojang") // root

    mc1144.link(mc1152, null)
    mc1152.link(mc1165, null)
    mc1165.link(mc1171, null)
    mc1171.link(mc1182, null)
    mc1182.link(mc1192, null)
    mc1192.link(mc1193, file("versions/mapping-1.19.2-1.19.3.txt"))
}

String realVersion = "${project.mod_version}"

if (project.pre_release != "") {
    realVersion += "-${project.pre_release}+"
    realVersion += grgit.head().abbreviatedId
}

setVersion(realVersion)

tasks.register("cleanPreprocessSources") {
    doFirst {
        subprojects {
            def path = project.projectDir.toPath().resolve('build/preprocessed')
            path.toFile().deleteDir()
        }
    }
}