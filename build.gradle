plugins {
    id("maven-publish")
    id("fabric-loom").version("1.1-SNAPSHOT").apply(false)
    // https://github.com/Juuxel/LoomQuiltflower
    id("io.github.juuxel.loom-quiltflower").version("1.8.0").apply(false)
    id("net.kyori.blossom").version("1.3.1").apply(false)
    id("com.replaymod.preprocess").version("SNAPSHOT")
    id("org.ajoberstar.grgit").version("5.0.0")
}

preprocess {
    def mc1144 = createNode("1.14.4", 1_14_04, "mojang")
    def mc1152 = createNode("1.15.2", 1_15_02, "mojang")
    def mc1165 = createNode("1.16.5", 1_16_05, "mojang")
    def mc1171 = createNode("1.17.1", 1_17_01, "mojang")
    def mc1182 = createNode("1.18.2", 1_18_02, "mojang")
    def mc1192 = createNode("1.19.2", 1_19_02, "mojang")
    def mc1193 = createNode("1.19.3", 1_19_03, "mojang")
    def mc1194 = createNode("1.19.4", 1_19_04, "mojang") // root

    mc1144.link(mc1152, null)
    mc1152.link(mc1165, null)
    mc1165.link(mc1171, null)
    mc1171.link(mc1182, null)
    mc1182.link(mc1192, null)
    mc1192.link(mc1193, file("versions/mapping-1.19.2-1.19.3.txt"))
    mc1193.link(mc1194, null)
}

ext {
    env = System.getenv()

    getVersionGit = { List paths ->
        if (grgit == null) {
            return "nogit"
        }

        List latestCommits = paths.isEmpty() ? grgit.log(maxCommits: 1) : grgit.log(paths: paths, maxCommits: 1)
        return latestCommits.isEmpty() ? "uncommited" : "${latestCommits.get(0).id.substring(0, 7)}"
    }

    getBuildNumber = {
        return ext.env.GITHUB_RUN_NUMBER ? ext.env.GITHUB_RUN_NUMBER : Integer.MAX_VALUE
    }

    getVersionType = {
        switch (ext.env.BUILD_TYPE) {
            case "RELEASE":
                return "stable"
            case "BETA":
                return "beta"
            default:
                return "dev"
        }
    }

    getVersionPatch = { List paths ->
        if (grgit == null) {
            return 0
        }

        List latestCommits = paths.isEmpty() ? grgit.log() : grgit.log(paths: paths)
        return latestCommits.size()
    }

    getMavenArtifactVersion = {
        return ext.getVersionType() == "stable" ? "${project.mod_version}.${ext.getVersionPatch([])}" : project.version
    }
}

setVersion("${project.mod_version}.${project.getVersionPatch([])}+${project.getVersionGit([])}-${project.getVersionType()}")

tasks.register("cleanPreprocessSources") {
    it.setGroup("${project.mod_id}")

    doFirst {
        subprojects {
            def path = project.projectDir.toPath().resolve("build/preprocessed")
            path.toFile().deleteDir()
        }
    }
}
