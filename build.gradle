plugins {
    id("fabric-loom").version("0.11-SNAPSHOT")
    id("maven-publish")
    id("org.ajoberstar.grgit").version("4.1.0")
    id("com.modrinth.minotaur").version("1.2.1")
}

setSourceCompatibility(JavaVersion.toVersion(project.java_version))
setTargetCompatibility(JavaVersion.toVersion(project.java_version))
String real_version = "${project.mod_version}"

if (project.pre_release != "") {
    real_version += "-${project.pre_release}+"
    if (project.pre_release == "alpha" && !grgit.status().clean) {
        real_version += new Date().format("yyyyMMdd.HHmmss") + ".dirty." + grgit.head().abbreviatedId
    } else {
        real_version += grgit.head().abbreviatedId
    }
}

setArchivesBaseName("${project.archives_base_name}-${project.minecraft_version}")
setVersion(real_version)
setGroup(project.maven_group)

repositories {
    maven {
        url = "https://www.cursemaven.com"
    }
    maven {
        url = "https://api.modrinth.com/maven"
    }
    mavenCentral()
}

loom {
    accessWidenerPath.set(file(file("src/main/resources/masa_gadget_mod.accesswidener")))
}

dependencies {
    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings("net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_mappings}:v2")
    modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")
    modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
    modImplementation("curse.maven:malilib-303119:${project.malilib_version}")
    modImplementation("curse.maven:minihud-244260:${project.minihud_version}")
    modImplementation("curse.maven:tweakeroo-297344:${project.tweakeroo_version}")
    modImplementation("curse.maven:litematica-308892:${project.litematica_version}")
    modImplementation("curse.maven:itemscroller-242064:${project.itemscroller_version}")
    modImplementation("maven.modrinth:modmenu:${project.mod_menu_version}")
    modImplementation("curse.maven:carpet-tis-addition-397510:${project.tis_carpet_version}")
    // Runtime Deps
    modRuntimeOnly("curse.maven:ommc-454900:${project.ommc_version}")
    modRuntimeOnly("curse.maven:lazydfu-433518:${project.lazydfu_version}")
    modRuntimeOnly("curse.maven:in-game-account-switcher-232676:${project.in_game_account_switcher_version}")
    modRuntimeOnly("curse.maven:imblockerfabric-399225:${project.imblockerfabric}")
    // tis carpet need
    modRuntimeOnly("curse.maven:carpet-349239:${project.carpet_version}")
    runtimeOnly("org.yaml:snakeyaml:${project.snakeyaml_version}")
}

processResources {
    inputs.property("version", real_version)
    filesMatching("fabric.mod.json") {
        expand(["version": real_version])
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release.set(Integer.parseInt(project.java_version as String))
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    // withSourcesJar()
}

jar {
    from("LICENSE") {
        rename({ return "${it}_${project.archives_base_name}" })
    }
}

import com.modrinth.minotaur.TaskModrinthUpload

task publishModrinth(type: TaskModrinthUpload) {
    onlyIf {
        System.getenv("MODRINTH") // Only attempt to run this task if the MODRINTH variable is set, otherwise SKIP it
    }
    token = System.getenv("MODRINTH")
    changelog = System.getenv("CHANGELOG")
    projectId = "SFO4Ca80"
    versionNumber = "${project.mod_version}-${project.minecraft_version}"
    // On fabric, use "remapJar" instead of "jar"
    uploadFile = remapJar
    addGameVersion(project.minecraft_version as String)
    addLoader("fabric")
}
